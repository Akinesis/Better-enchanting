plugins {
    id 'dev.architectury.loom' version '1.6-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
}

architectury {
    minecraft = project.minecraft_version
}


subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered {
            it.mappings("net.fabricmc:yarn:$rootProject.yarn_mappings:v2")
            it.mappings("dev.architectury:yarn-mappings-patch-neoforge:$rootProject.yarn_mappings_patch_neoforge_version")
        }
    }

}


allprojects {

    apply plugin: "java"

    group = rootProject.maven_group
    version = rootProject.mod_version

    repositories {
        maven { url "https://maven.neoforged.net/releases/" }

        maven { url "https://maven.quiltmc.org/repository/release/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }


    tasks.withType(JavaCompile){
        options.encoding = "UTF-8"
        options.release.set(21)
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()
    }

}
